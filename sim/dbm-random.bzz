include "buzz_includes/buzz_utils/vec2.bzz"
include "buzz_includes/buzz_utils/string.bzz"
include "config/DBM-SMS/parameters.bzz"
include "buzz_includes/DBM-SMS/global_variables.bzz"
include "buzz_includes/debug.bzz"


ARENA_SIZE_X = 50
ARENA_SIZE_Y = 50

AXIS_X = 0
AXIS_Y = 1

step_counter = 0
stigmergy_risk
stigmergy_exploration


function init_stigmergies() {
  stigmergy_risk = stigmergy.create(1)
  stigmergy_risk.onconflict(function(k, l, r){
    merged = l
    merged_data = l.data * 0.5 + r.data * 0.5
    merged.data = merged_data
    return merged
    })

  stigmergy_exploration = stigmergy.create(2)
  stigmergy_exploration.onconflict(function(k, l, r){
    merged = l
    merged_data = l.data * 0.5 + r.data * 0.5
    merged.data = merged_data
    return merged
    })

  if (id == 0) {
    var i = 0
    var j = 0
    while (i < ARENA_SIZE_X) {
      j = 0
      while (j < ARENA_SIZE_Y) {
        key = string.concat(string.tostring(i), "_", string.tostring(j))

        stigmergy_risk.put(key, nil)
        stigmergy_exploration.put(key, 0)

        j = j + 1
      }
      i = i + 1
    }
  }
}

function update_risk() {
  stigmergy_risk.put(get_current_key(), get_radiation_intensity())  # Put timestamp in exploration map
}

function update_exploration() {
  stigmergy_exploration.put(get_current_key(), step_counter)  # Put timestamp in exploration map
}

# Executed at init time
function init() {
  # Take off command for the Spiri drone model
  takeoff()

  # Show trajectory for debugging
  show_colored_trajectory(NUMBER_OF_ROBOTS)

  # Set first search position
  next_position.x = uniform(SEARCH_SPACE.minx, SEARCH_SPACE.maxx)
  next_position.y = uniform(SEARCH_SPACE.miny, SEARCH_SPACE.maxy)

  init_stigmergies()
}

# Random walk search
function random_search()
{ 
  next_position.x = uniform(math.max(next_position.x-SEARCH_SPEED, SEARCH_SPACE.minx), math.min(next_position.x+SEARCH_SPEED, SEARCH_SPACE.maxx))
  next_position.y = uniform(math.max(next_position.y-SEARCH_SPEED, SEARCH_SPACE.miny), math.min(next_position.y+SEARCH_SPEED, SEARCH_SPACE.maxy))
  goto(next_position.x, next_position.y)
}

# Executed every time step
function step() {
  if (current_step < EXPERIMENT_LENGHT) {
    if (current_step % NEW_POSE_DELAY) {
      update_exploration()
      update_risk()

      # Find next position
      random_search()
    }
    current_step = current_step + 1
  } 
  else if (current_step == EXPERIMENT_LENGHT and id == 0) {
    stigmergy_risk.foreach(function(key, data, robot){
      log_datum(key, data)
    })
  }

  step_counter = step_counter + 1
}
 
# Execute at exit
function destroy() {
}
