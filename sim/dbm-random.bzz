include "buzz_includes/buzz_utils/vec2.bzz"
include "buzz_includes/buzz_utils/string.bzz"
include "config/DBM-SMS/parameters.bzz"
include "buzz_includes/DBM-SMS/global_variables.bzz"
include "buzz_includes/debug.bzz"


ARENA_SIZE_X = 50
ARENA_SIZE_Y = 50

var stigmergy_risk
var stigmergy_exploration


function init_stigmergies() {
  stigmergy_risk = stigmergy.create(1)
  stigmergy_risk.onconflict(function(k, l, r){
    merged = l
    merged_data = l.data * 0.5 + r.data * 0.5
    merged.data = merged_data
    return merged
    })

  stigmergy_exploration = stigmergy.create(2)
  stigmergy_exploration.onconflict(function(k, l, r){
    merged = l
    merged_data = l.data * 0.5 + r.data * 0.5
    merged.data = merged_data
    return merged
    })
}

function update_risk() {
  stigmergy_risk.put(get_current_key(), get_radiation_intensity())  # Put timestamp in exploration map
}

function update_exploration() {
  stigmergy_exploration.put(get_current_key(), current_step)  # Put timestamp in exploration map
}

function exploration_gradient(adjacent_key) {
  if (stigmergy_exploration.get(adjacent_key) != nil) {
    return current_step - stigmergy_exploration.get(adjacent_key)
  } else {
    return current_step
  }
}

function risk_gradient(adjacent_key) {
  if (stigmergy_risk.get(adjacent_key) != nil) {
    return stigmergy_risk.get(current_key) - stigmergy_risk.get(adjacent_key)
  } else {
    return 0.0
  }
}

function goto_gradient(gradient_function){
  var i = -1
  var j = -1
  var vec_goto = math.vec2.new(0.0, 0.0)

  while (i <= 1){
    j = -1
    while (j <= 1){
      if (i != 0 and j != 0){
        key_x = string.toint(string.charat(get_current_key(), 0)) + i
        key_y = string.toint(string.charat(get_current_key(), 2)) + j
        adjacent_key = string.concat(key_x, "_", key_y)

        gradient_intensity = gradient_function(adjacent_key)
        
        vec_goto = math.vec2.add(vec_goto, math.vec2.scale(math.vec2.norm(math.vec2.new(i, j), gradient_intensity)))
      }
      j = j + 1
    }
    i = i + 1
  }

  return math.vec2.norm(vec_goto)
}

# Executed at init time
function init() {
  # Take off command for the Spiri drone model
  takeoff()

  # Show trajectory for debugging
  show_colored_trajectory(NUMBER_OF_ROBOTS)

  # Set first search position
  next_position.x = uniform(SEARCH_SPACE.minx, SEARCH_SPACE.maxx)
  next_position.y = uniform(SEARCH_SPACE.miny, SEARCH_SPACE.maxy)

  init_stigmergies()
}

# Random walk search
function random_search()
{ 
  next_position.x = uniform(math.max(next_position.x-SEARCH_SPEED, SEARCH_SPACE.minx), math.min(next_position.x+SEARCH_SPEED, SEARCH_SPACE.maxx))
  next_position.y = uniform(math.max(next_position.y-SEARCH_SPEED, SEARCH_SPACE.miny), math.min(next_position.y+SEARCH_SPEED, SEARCH_SPACE.maxy))
  goto(next_position.x, next_position.y)
}

# Executed every time step
function step() {
  if (current_step < EXPERIMENT_LENGHT) {
    if (current_step % NEW_POSE_DELAY) {
      update_exploration()
      update_risk()

      # Find next position
      random_search()
    }
    current_step = current_step + 1
  } 
  else if (current_step == EXPERIMENT_LENGHT and id == 0) {
    stigmergy_risk.foreach(function(key, data, robot){
      log_datum(key, data)
    })
  }
}
 
# Execute at exit
function destroy() {
}
