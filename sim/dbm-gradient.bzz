include "buzz_includes/buzz_utils/vec2.bzz"
include "buzz_includes/buzz_utils/string.bzz"
include "config/DBM-SMS/parameters.bzz"
include "buzz_includes/DBM-SMS/global_variables.bzz"
#include "buzz_includes/DBM-SMS/dispersion.bzz"
include "buzz_includes/debug.bzz"


EXPLORATION_WEIGHT = 1.0
RISK_WEIGHT = 2.0

var stigmergy_risk
var stigmergy_exploration


function log_metrics(){
  if (debug.msgqueue != nil) {
    log_datasize(debug.msgqueue.total, current_step)
  }
  if (id == 0) {
    stigmergy_risk.foreach(function(key, data, robot){
      log_datum(key, data, current_step)
    })
  }
}

function key_to_vec2(key) {
  split_key = string.split(get_current_key(), "_")
  x = string.toint(split_key[0])
  y = string.toint(split_key[1])

  return math.vec2.new(x, y)
}

function init_stigmergies() {
  stigmergy_risk = stigmergy.create(1)
  stigmergy_risk.onconflict(function(k, l, r){
    return l
    })

  stigmergy_exploration = stigmergy.create(2)
  stigmergy_exploration.onconflict(function(k, l, r){
    return l
    })
}

function update_risk() {
  new_value = get_radiation_intensity()
  current_key = get_current_key()
  if (stigmergy_risk.get(current_key) != nil){
    new_value = 0.5 * new_value + 0.5 * stigmergy_risk.get(current_key)
  }
  stigmergy_risk.put(current_key, new_value)
}

function update_exploration() {
  stigmergy_exploration.put(get_current_key(), current_step)  # Put timestamp in exploration map
}

function exploration_gradient(adjacent_key) {
  if (stigmergy_exploration.get(adjacent_key) != nil) {
    return current_step - stigmergy_exploration.get(adjacent_key)
  } else {
    return current_step
  }
}

function risk_gradient(adjacent_key) {
  if (stigmergy_risk.get(adjacent_key) != nil) {
    return get_radiation_intensity() - stigmergy_risk.get(adjacent_key)
  } else {
    return 0.0
  }
}

function get_adjacent_key(i, j) {
  split_key = string.split(get_current_key(), "_")
  key_x = string.toint(split_key[0]) + i
  key_y = string.toint(split_key[1]) + j

  return string.concat(string.tostring(key_x), "_", string.tostring(key_y))
}

# Safely normalize a vector. 
# If input vector is (0, 0), return a random vector to avoid division by zero.
function safe_normalize(vector) {
  if (vector.x == 0 and vector.y == 0) {
    return math.vec2.new(math.rng.uniform(-0.5, 0.5), math.rng.uniform(-0.5, 0.5))
  } else {
    return math.vec2.norm(vector)
  }
}

function gradient(gradient_function){
  var i = -1
  var j = -1
  var gradient_vec = math.vec2.new(0.0, 0.0)

  while (i <= 1){
    j = -1
    while (j <= 1){
      if (not(i == 0 and j == 0)){
        gradient_intensity = gradient_function(get_adjacent_key(i, j))
        adjacent_gradient = math.vec2.scale(math.vec2.norm(math.vec2.new(i, j)), gradient_intensity)

        gradient_vec = math.vec2.add(gradient_vec, adjacent_gradient)
      }
      j = j + 1
    }
    i = i + 1
  }

  return safe_normalize(gradient_vec)
}

# Executed at init time
function init() {
  # Take off command for the Spiri drone model
  takeoff()

  # Show trajectory for debugging
  show_colored_trajectory(NUMBER_OF_ROBOTS)

  previous_position = key_to_vec2(get_current_key())

  init_stigmergies()
  failed = 0
}

function move() {
  exploration = math.vec2.scale(gradient(exploration_gradient), EXPLORATION_WEIGHT)
  risk = math.vec2.scale(gradient(risk_gradient), RISK_WEIGHT)

  norm_direction = safe_normalize(math.vec2.add(exploration, risk))
  scaled_direction = math.vec2.scale(norm_direction, SEARCH_SPEED)

  previous_position = key_to_vec2(get_current_key())
  next_position = math.vec2.add(previous_position, scaled_direction)

  goto(next_position.x, next_position.y)
}

function failure() {
  if (get_radiation_intensity() >= math.rng.uniform(0.0, 1.0)) {
    log("I failed")
    log(get_radiation_intensity())
    return 1
  }
  return 0
}

# Executed every time step
function step() {
  if (current_step < EXPERIMENT_LENGHT and failed == 0) {
    if (current_step % NEW_POSE_DELAY) {
      update_exploration()
      update_risk()

      move()
      failed = failure()
    }
    log_metrics()
  }
  current_step = current_step + 1
}

function reset() {
}
 
# Execute at exit
function destroy() {
}
